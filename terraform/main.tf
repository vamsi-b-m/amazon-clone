# terraform/main.tf

#  ------- VPC ------------------------------------------------------------
resource "aws_vpc" "main" {
  cidr_block = "10.0.0.0/16"
  enable_dns_hostnames = true
  enable_dns_support = true

  tags = {
    Name = "${var.project_name}-vpc"
    Environment = var.environment
    Project = var.project_name
  }
}

#  ------- Internet Gatway ------------------------------------------------------------
resource "aws_internet_gateway" "main" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name    = "${var.project_name}-igw"
    Environment = var.environment
  }
}

#  ------- Public Subnet ------------------------------------------------------------
resource "aws_subnet" "public" {
  vpc_id = aws_vpc.main.id
  cidr_block = "10.0.1.0/24"
  availability_zone = "${var.aws_region}a"
  map_public_ip_on_launch = true
  tags = {
    Name    = "${var.project_name}-public-subnet"
    Environment = var.environment
  }
}

#  ------- Route Table ------------------------------------------------------------
resource "aws_route_table" "public" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.main.id
  }

  tags = {
    Name        = "${var.project_name}-public-rt"
    Environment = var.environment
  }
}

resource "aws_route_table_association" "public" {
  subnet_id = aws_subnet.public.id
  route_table_id = aws_route_table.public.id
}

#  ------- Security Group ------------------------------------------------------------
resource "aws_security_group" "ec2" {
  name = "${var.project_name}-ec2-sg"
  description = "Security group for Amazon Clone EC2"
  vpc_id = aws_vpc.main.id

  # SSH
  ingress {
    from_port = 22
    to_port = 22
    protocol = "tcp"
    cidr_blocks = [var.allowed_ssh_ip]
    description = "SSH access"
  }

  # Frontend
  ingress {
    from_port = 3000
    to_port = 3000
    protocol = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
    description = "Frontend"
  }

  # API Gateway
  ingress {
    from_port = 4000
    to_port = 4000
    protocol = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
    description = "API Gateway"
  }

  # HTTP
  ingress {
    from_port = 80
    to_port = 80
    protocol = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
    description = "HTTP"
  }

  # RabbitMQ Management - restricted
  ingress {
    from_port = 15672
    to_port = 15672
    protocol = "tcp"
    cidr_blocks = [var.allowed_ssh_ip]
    description = "RabbitMQ Management UI"
  }

  # All outbound
  egress {
    from_port = 0
    to_port = 0
    protocol = "-1"
    cidr_blocks = ["0.0.0.0/0"]
    description = "All outbound traffic"
  }

  tags = {
    Name = "${var.project_name}-ec2-sg"
    Environment = var.environment
  }
}

#  ------- EC2 Instance ------------------------------------------------------------
resource "aws_instance" "app_server" {
  ami = var.ami_id
  instance_type = var.instance_type
  subnet_id = aws_subnet.public.id
  vpc_security_group_ids = [aws_security_group.ec2.id]
  key_name = var.key_pair_name
  
  tags = {
    Name    = "${var.project_name}-server"
    Environment = var.environment
    Project = var.project_name
  }
}

#  ------- Elastic IP ------------------------------------------------------------
resource "aws_eip" "app_server" {
  instance = aws_instance.app_server.id
  domain = "vpc"

  tags = {
    Name = "${var.project_name}-eip"
    Environment = var.project_name
  }
}
